buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}


apply plugin: 'com.android.application'

dependencies {
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android:flexbox:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.android.billingclient:billing:4.0.0'
    implementation 'androidx.fragment:fragment:1.3.6'

    // implementation 'uk.co.samuelwall:material-tap-target-prompt:3.0.0'
    // implementation project(':externals:ShowcaseView:library')
    // implementation 'com.xenione.libs:tab-digit:1.0.2'
    implementation project(':externals:android-preferences')
    implementation project(':externals:tab-digit:digit')
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.github.prolificinteractive:material-calendarview:1.6.1'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation 'androidx.mediarouter:mediarouter:1.2.4'
    implementation 'com.google.android.gms:play-services-cast-framework:20.0.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'org.shredzone.commons:commons-suncalc:2.11'
    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.14.2'
    implementation 'com.android.support:multidex:1.0.3'
}

def getApiKeyOWM() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("apikey.owm")
}

def getApiKeyDarkSky() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("apikey.darksky")
}

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.2'

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    buildTypes {
        release {
            lintOptions {
                checkReleaseBuilds false
                disable 'MissingTranslation'
                abortOnError false
            }
            proguardFile '/home/stefan/android/projects/nightdream/proguard-project.txt'
            minifyEnabled true
            shrinkResources true
        }
        debug {
            proguardFiles 'proguard-project.txt'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    defaultConfig {
        minSdkVersion 17
        buildConfigField "String", "API_KEY_OWM", "\""+getApiKeyOWM()+"\""
        buildConfigField "String", "API_KEY_DARK_SKY", "\""+getApiKeyDarkSky()+"\""
        targetSdkVersion 30
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildFeatures {
        dataBinding true
    }
}
