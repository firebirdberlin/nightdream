plugins {
    id 'com.android.application'
}

def getApiKeyOWM() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("apikey.owm")
}

android {
    namespace = 'com.firebirdberlin.nightdream'
    compileSdk = 36

    flavorDimensions "version"

    productFlavors {
        // Flavor for the version WITH Google Play Services
        full {
            dimension "version"
        }

        // Flavor for the version WITHOUT Google Play Services
        noGms {
            dimension "version"
            // set a different app ID so that we can install both versions in parallel
            applicationIdSuffix ".noGms"
        }
    }

    defaultConfig {
        applicationId "com.firebirdberlin.nightdream"
        minSdk 24
        targetSdk 36
        versionCode 342
        versionName "2.11.20"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        buildConfigField "String", "API_KEY_OWM", "\""+getApiKeyOWM()+"\""
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            lintOptions {
                checkReleaseBuilds = false
                disable 'MissingTranslation'
                abortOnError = false
            }
            proguardFile 'proguard-project.txt'
            minifyEnabled = true
            shrinkResources = true
        }
        debug {
            proguardFiles 'proguard-project.txt'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig = true
        dataBinding = true
        renderScript = false
        aidl = true
    }
}

dependencies {
    // AndroidX Libraries
    implementation 'androidx.activity:activity-ktx:1.12.2'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.3.0"
    implementation 'androidx.exifinterface:exifinterface:1.4.2'
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0"
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.work:work-multiprocess:2.11.0'
    implementation 'androidx.work:work-runtime-ktx:2.11.0'

    // Google Libraries
    implementation 'androidx.media3:media3-exoplayer:1.9.0'
    implementation 'androidx.media3:media3-ui:1.9.0'
    implementation 'androidx.media3:media3-session:1.9.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.9.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.code.gson:gson:2.13.2'

    // Third-party Libraries
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.firebirdberlin:android-preferences:36.0.5'
    implementation 'com.github.firebirdberlin:tab-digit:36.0.2'
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.0.1'
    implementation 'com.github.prolificinteractive:material-calendarview:1.6.1'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'org.shredzone.commons:commons-suncalc:3.11'

    // Full flavor specific dependencies
    fullImplementation 'com.android.billingclient:billing:8.3.0'
    fullImplementation 'com.google.android.gms:play-services-cast-framework:22.2.0'
    fullImplementation 'androidx.media3:media3-cast:1.9.0'

    // Multidex support
    // https://developer.android.com/studio/build/multidex
    implementation "androidx.multidex:multidex:2.0.1"

    // Testing Libraries
    testImplementation 'junit:junit:4.13.2'
}
